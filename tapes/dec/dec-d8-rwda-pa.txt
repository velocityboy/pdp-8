/ PDP-8 DISK MONITOR SYSTEM RESTORE PROGRAM
/ SINGLE DISK DF32 VERSION (OCT. 15, 1969)
/
/ COPYRIGHT 1969, DIGITAL EQUIPMENT CORP., MAYNARD, MASS.
/
*200
/
START,	CLA CLL
	TAD	(7576		/READ MONITOR HEAD INTO 7600
	DCA I	(7750
	TAD	(7576
	DCA I	(7751
	DMAR
	DFSC
	JMP	.-1
	DFSE
	HLT
	JMS I	(7642		/GET DN1 BACKUP
		3
		3
DN1,		DNBUF
		0
	HLT
	JMS I	(7642		/GET SAM1 BACKUP
		3
		4
		DNBUF+200
SMLINK,		0
	HLT
	TAD	SMLINK
	DCA	NUSMLK
	JMS I	(7642		/RESTORE DN1
		5
		177
		DNBUF
		201
	HLT
	JMS I	(7642		/RESTORE SAM1
		5
		200
		DNBUF+200
NUSMLK,		0
	HLT
	TAD	(-200		/REFRESH DN2
	DCA	CTR
	TAD	DN1
	DCA	PTR
	DCA I	PTR
	ISZ	PTR
	ISZ	CTR
	JMP	.-3
	JMS I	(7642
		5
		201
		DNBUF
		202
	HLT
/
/INCLUDE NEXT 2 LINES IF YOU HAVE A 2, 3, OR 4 UNIT DF32
/XTRA,	IAC			/MARK SAMBLOCK SLOT
/	DCA I	(DNBUF+1
/
/INCLUDE NEXT 6 LINES IF YOU HAVE A 4 UNIT DF32
/	JMS I	(7642		/REFRESH SAM3
/		5
/		1001
/		DNBUF
/		1401
/	HLT
/
/INCLUDE NEXT 12 LINES IF YOU HAVE A 3 OR 4 UNIT DF32
/	JMS I	(7642		/REFRESH SAM2
/		5
/		401
/		DNBUF
/		1001
/	HLT
/	TAD	(100		/MARK NON-EXISTENT BLOCKS
/	DCA I	(DNBUF+172
/	TAD	(100
/	DCA I	(DNBUF+173
/	TAD	XTRA+6		/SET LAST SAMBLOCK NUMBER
/	DCA	LASTSM
/
/INCLUDE NEXT 4 LINES IF YOU HAVE A 4 UNIT DF32
/	TAD	(100		/MARK NON-EXISTENT BLOCKS
/	DCA I	(DNBUF+170
/	TAD	(100
/	DCA I	(DNBUF+171
/
/INCLUDE NEXT 15 LINES IF YOU HAVE A 2, 3, OR 4 UNIT DF32
/	TAD	(100		/MARK NON-EXISTENT BLOCKS
/	DCA I	(DNBUF+174
/	TAD	(100
/	DCA I	(DNBUF+175
/	TAD	(100
/	DCA I	(DNBUF+176
/	TAD	(100
/	DCA I	(DNBUF+177
/	JMS I	(7642		/REFRESH LAST SAMBLOCK
/		5
/LASTSM,	401
/		DNBUF
/		0
/	HLT
/	DCA I	(DNBUF+1	/CLEAR
/
/END OF DF32 CONDITIONAL CODE
/
	TAD	(-74		/REFRESH DN3
	DCA	CTR
	TAD	(DNBUF+104
	DCA	PTR
	TAD	(100
	DCA I	PTR
	ISZ	PTR
	ISZ	CTR
	JMP	.-4
	JMS I	(7642
		5
		202
		DNBUF
		0
	HLT
	JMP I	(7600		/GO TO MONITOR
/
/END
/
/
/DEFINITIONS
/
DMAR=6603
DSFE=6621
DFSC=6622
CTR=START
PTR=START+1
DNBUF=400
/
$