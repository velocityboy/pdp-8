
/DIGITAL 8-21-F
/TWO'S COMPLEMENT, SINGLE PRECISION,
/SIGNED MULTIPLY ROUTINE WITH EAE.
/RETURN HIGH ORDER PRODUCT IN AC,
/LOW IN MP1.
MULT,     0
          SZA                 /TEST FOR ZERO MULTIPLIER.
          JMP .+3
          DCA MP1             /LOW ORDER PRODUCT.
          JMP MPZ
          CLL
          SPA                 /TEST FOR NEGATIVE MULTIPLIER.
          CMA CML IAC
          MQL                 /MULTIPLIER TO MQ.
          TAD I MULT
          SNA                 /TEST FOR ZERO MULTIPLICAND.
          JMP MULT+3
          SPA                 /TEST FOR NEGATIVE MULTIPLICAND.
          CMA CML IAC
          DCA .+4             /STORE MULTIPLICAND.
          RAL
          DCA MP1             /SAVE LINK.
          MUY
MP2,      0                   /MULTIPLICAND.
          DCA MP2             /SAVE HIGH ORDER PRODUCT.
          TAD MP1
          RAR CLL             /RESTORE LINK.
          MQA                 /LOW ORDER PRODUCT TO AC.
          SNL                 /TEST SIGN OF PRODUCT.
          JMP .+10
          CLL CMA IAC         /COMPLEMENT PRODUCT.
          DCA MP1             /LOW ORDER PRODUCT.
          TAD MP2             /HIGH ORDER PRODUCT.
          CMA
          SZL
          IAC
          JMP MPZ
          DCA MP1             /LOW ORDER PRODUCT
          TAD MP2             /HIGH ORDER PRODUCT
MPZ,      ISZ MULT            /SET UP FOR EXIT.
          JMP I MULT          /EXIT.
MP1,      0

          $

